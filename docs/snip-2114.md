---
title: ERC-2114 Token Standard
author: kahi <kahi@seraphlabs.io>
status: Draft
type: Standards Track
category: SRC
created: $SNIP_DATE
---

## Table of Contents

-   [Abstract](#abstract)
    -   [Trait Catalogue](#trait-catalogue)
    -   [Attribute System](#attribute-system)
    -   [Tokens owning tokens](#tokens-owning-tokens)
-   [Motivation](#motivation)
-   [Specification](#specification)
    -   [Enums](#enums)
        -   [AttributeType](#attributetype)
    -   [Events](#events)
        -   [TraitCatalogAttached](#traitcatalogattached)
        -   [ScalarTransfer](#scalartransfer)
        -   [ScalarRemove](#scalarremove)
        -   [AttributeCreated](#attributecreated)
        -   [TokenAttributeUpdate](#tokenattributeupdate)
    -   [Interface](#interface)
-   [Trait Catalog](#trait-catalog)
    -   [Events](#events-1)
        -   [TraitListUpdate](#traitlistupdate)
    -   [Interface](#interface-1)
-   [Attribute Extensions](#attribute-extensions)
    -   [Slot Attribute](#slot-attribute)
    -   [Events](#events-2)
        -   [SlotAttributeUpdate](#slotattributeupdate)
    -   [interface](#interface-2)
-   [Inventory Extension](#inventory-extension)
    -   [BackPack](#backpack)
    -   [Inventory](#inventory)
    -   [Attribute Inheritance Model](#attribute-inheritance-model)
    -   [Specification](#specification-1)
    -   [Events](#events-3)
        -   [InventorySlotCriteria](#inventoryslotcriteria)
        -   [InventoryAttributes](#inventoryattributes)
        -   [InventoryUpdated](#inventoryupdated)
    -   [Interface](#interface-3)
    -   [Inventory Manipulation](#inventory-manipulation)
-   [Implementation](#implementation)
-   [History](#history)
-   [Copyright](#copyright)

## Abstract

ERC2114 was created to be an extension of ERC3525 or ERC721 to aid in solving the limitations of these token standards when it comes to GameFi. With this proposal we are looking to solve accomplish:

1. Making metadata dynamic and changeable
2. The ability for tokens to own other tokens (i.e. equipping/unequipping) and by its nature inherit any attributes/metadata held by its tokens.

### Trait Catalogue

In this proposal, tokens utilize their attached `attr_id` to define its traits values, but it becomes problematic if all attributes can have any corresponding value. However, with the `TraitCatalog` interface users can set predefined values that a `attr_id` can have.

Trait Catalogues generates and stores a trait list in the form of `list_ids` which are identifiers that points to a pre generated list of values that can be attached to `attr_ids` of type `String` on creation. Thus, the attributes corresponding values to the values in the trait list.`list_ids` are reusable and can be attached to multiple `attr_ids`, allowing different attributes to share the same set of values in a trait list.

### Attribute System

With this extension we propose storing metadata in the form of `attr_id` which will represent the traits of a token. Every `attr_id` has a name and a type `<String, Number>` and when attached to a token it can have a corresponding value, acting like key value pair for a tokensâ€™ trait `name:value`.

`Number` type attributes can contain `decimal` property to indicate the number of decimals the corresponding value of a tokens `attr_id` uses.

`String` type attributes can contain `list_id` property to indicate whether the attributes corresponding values is derived from a trait list in the `TraitCatalog` or is just any string.

There are 3 attribute models

1. Token Attributes : attributes that can be added to tokens to define its traits
2. Slot Attributes : attributes that are assigned to slots `ERC3525: slot_id`. allowing any token minted under the same slot to automatically inherit its slots attributes.
3. Inventory Attribute : attributes that are assigned to a slot allowing any token from that slot to inherit those attributes from its inventory

### Tokens owning tokens

In this proposal we suggest that NFTs/SFTs ownership is not just limited to addresses but to other tokenIds, allowing NFTs/SFTs to inherit attributes from its owned tokenIds or act as an inventory/equipment system for NFTs/SFTs.

## Motivation

## Specification

**NOTES**:

-   The following specifications use syntax from Cairo `2.1.0` (or above)

### Enums

#### AttributeType

-   `Empty`: Default value for `attr_ids` that have yet to be created
-   `String`: values of this attribute type should be treated as a string, can be attached with a `list_id` to have the attribute be limited to a predefined set of values determined by the trait list
-   `Number`: values of this attribute type should be treated as a number, can be attached with a decimal for its value

```cairo
#[derive(Copy, Drop, Serde, PartialEq, starknet::Store, Default)]
enum AttrType {
   #[default]
    Empty,
    String: u64,
    Number: u8,
}
```

### Events

#### TraitCatalogAttached

MUST emit when a TraitCatalog contract is added

```cairo
#[derive(Drop, starknet::Event)]
struct TraitCatalogAttached {
    #[key]
    from: ContractAddress,
    #[key]
    trait_catalog_addr: ContractAddress,
}
```

#### ScalarTransfer

MUST emit when `tokenId` is transfered by operator/owner `from` into another token `to_token_id`

```cairo
#[derive(Drop, starknet::Event)]
struct ScalarTransfer {
    #[key]
    from: ContractAddress,
    #[key]
    token_id: u256,
    #[key]
    to_token_id: u256
}
```

#### ScalarRemove

MUST emit when `token_id` is transfered out of a token `from_token_id` back to an address

```cairo
#[derive(Drop, starknet::Event)]
struct ScalarRemove {
    #[key]
    from_token_id: u256,
    #[key]
    token_id: u256,
    #[key]
    to: ContractAddress
}
```

#### AttributeCreated

MUST emit when an `attr_id` is instantiated

```cairo
#[derive(Drop, starknet::Event)]
struct AttributeCreated {
    #[key]
    attr_id: u64,
    #[key]
    attr_type: AttrType,
    name : felt252
}
```

#### TokenAttributeUpdate

MUST emit when an `attr_id` is added to a `token_id` or when its corresponding value is changed

if `attr_id` is of type `String`:

-   `list_id == 0` `old_value`/`new_value` represents the actual value of the attribute
-   `list_id != 0` `old_value`/`new_value`represents the index of the trait list attached to the`attr_id`

```cairo
#[derive(Drop, starknet::Event)]
struct TokenAttributeUpdate {
    #[key]
    token_id : u256,
    #[key]
    attr_id: u64,
    #[key]
    attr_type: AttrType,
    #[key]
    old_value : felt252,
    #[key]
    new_value : felt252
}
```

### Interface

```cairo
#[starknet::interface]
trait IERC2114<TContractState> {
    // @notice gets the TraitCatalog contract attached to this contract
    // @return the contract address of the TraitCatalog contract
    fn get_trait_catalog(self: @TContractState) -> ContractAddress;

    // @notice gets the token balance of another token
    // @param `token_id` the token to query token balance from
    // @return the total ammount of tokens owned by `token_id`
    fn token_balance_of(self: @TContractState, token_id: u256) -> u256;

    // @notice get the parent token of `token_id`
    // @param `token_id` the token to query token ownership of
    // @return the token ID that currently owns `token_id`
    fn token_of(self: @TContractState, token_id: u256) -> u256;

    // @notice get the token at the specified index of all tokens owned by `token_id`.
    // @param `token_id` token id to query tokens from
    // @param `index` the index in the list of tokens owned by `token_id`
    // @return the token ID at `index` of all child tokens in `token_id`
    fn token_of_token_by_index(self: @TContractState, token_id: u256, index: u256) -> u256;

    // @notice gets the name associated with `attr_id`
    // @param `attr_id` attribute id to query name from
    // @return the name of the `attr_id` as a felt
    fn attribute_name(self: @TContractState, attr_id: u64) -> felt252;

    // @notice get the attribute type of `attr_id`
    // @dev this function SHOULD return an enum of either `<Empty, Number(u8), String(u64)>`
    //      Empty -> means `attr_id` has yet to be created
    //      Number(u8) -> means `attr_id` values should be treated as numbers
    //                  the u8 in the enum represents the decimals that the attribute value uses
    //      String(u64) -> means `attr_id` values should be treated as a string
    //                    the u64 indicates whether a trait list is used to derive the attribute value
    // @param `attr_id` attribute id to query type from
    // @return the type of the attribute
    fn attribute_type(self: @TContractState, attr_id: u64) -> AttrType;

    // @notice get the attribute value associated with the token
    // @dev if `attr_id` uses a trait list SHOULD still output the value acording to the trait list
    //       SHOULD NOT return the index from the trait list
    // @param `token_id` token to query the attributes value from
    // @param `attr_id` attribute id to query value from
    // @return the attribute value associated with the token
    fn attribute_value(self: @TContractState, token_id: u256, attr_id: u64) -> felt252;

    // @notice get all attributes associated with `token_id`
    // @param `token_id` token which to query all owned attributes id
    // @return an array of all `attr_id` attached to a token
    fn attributes_of(self: @TContractState, token_id: u256) -> Span<u64>;

    // @notice transfer a token into another token
    // @dev caller MUST be owner of `token_id` or an approved operator
    //   MUST revert if `token_Id` is already owned by another token
    //   MUST revert if from address is 0 or is not owner of `token_id`
    //   MUST revert if `token_id` or `to_token_id` is zero
    //   SHOULD transfer token to an abrtirary address or this contract address
    //   MUST emit the `ScalarTransfer` event
    // @param `from` current owner of `token_id` that is to be transfered
    // @param `token_id` token to transfer
    // @param `to_token_id` token that will be the new owner of the transfered token
    fn scalar_transfer_from(
        ref self: TContractState, from: ContractAddress, token_id: u256, to_token_id: u256
    );

    // @notice transfers out a "child token" from its "Parent token" that currently owns it
    // @dev caller MUST be either the owner or approved operator of the "final parent token"
    //   of the `from_token_id`. which means that approval falls onto the final token id; up the
    //   chain of token ownership that isnt currently owned by any token id.
    //   MUST revert if `from_token_id` or `token_id` is zero
    //   MUST revert if `from_token_Id` is not the direct owner of `token_id`
    //   SHOULD transfer token back to the owner address of the "final parent token"
    //   MUST emit the `ScalarRemove` event
    // @param `from_token_id` the "parent token" that you want to transfer out the "child token"
    // @param 'token_id' "child token" that you want to remove from token id that owns it
    fn scalar_remove_from(ref self: TContractState, from_token_id: u256, token_id: u256);

    // @notice instantiates an attribute id into existance
    // @dev SHOULD revert if caller is not owner or an appproved operator of the contract
    //  MUST revert if `attr_id` is 0 or already exist
    //  MUST revert if `attr_type` is `EMPTY`
    //  MUST revert if `attr_type` is `String(u64)` and its held list id dosent exist and is not zero
    //  MUST revert if `name` is zero
    //  MUST emit the `AttributeCreated` event
    // @param `attr_id` attribute id that is to be created
    // @param `attr_type` type of vlaue that the attribute should return
    // @param `name` the decriptive name for the attribute id
    fn create_attribute(ref self: TContractState, attr_id: u64, attr_type: AttrType, name: felt252);
}
```

## Trait Catalog

**NOTES**:

-   interface of the Trait Catalog containg `list_id` that are attachable to `attr_Id` to limit its possible values based on its trait list
-   this interface SHOULD be added to the standard `ERC2114` contract on deployment
-   RECOMMENDED `trait_list` indexes starts from 1 as not to clash with default 0 index value owned by token ids

### Events

#### TraitListUpdate

MUST emit when new value is added to a `list_id` or when is changed

```cairo
#[derive(Drop, starknet::Event)]
struct TraitListUpdate {
    #[key]
    list_id : u64,
    #[key]
    index: felt252,
    #[key]
    old_value : felt252,
    #[key]
    new_value : felt252
}
```

### Interface

```cairo
#[starknet::interface]
trait ITraitCatalog<TContractState> {
    // @notice gets the total number of list that has been instantisated
    // @dev last list_id that was created is the trait list count as list ids are
    //   created in an ascending order
    // @return the total ammount of list ids that have been created
    fn trait_list_count(self: @TContractState) -> u64;

    // @notice gets the total number of values held within a trait list
    // @param `list_id` the trait list id to query length from
    // @return the length of the trait list
    fn trait_list_length(self: @TContractState, list_id: u64) -> felt252;

    // @notice get value in a trait list by its index
    // @dev if return values is 0 SHOULD treat it as index does not exist for trait list
    //  SHOULD return 0 if index is 0 as index for values SHOULD start at 1
    // @param `list_id` the trait list id to query values from
    // @param `index` the index in the trait list to query values from
    // @return value at index of trait list of `list_id`
    fn trait_list_value_by_index(self: @TContractState, list_id: u64, index: felt252) -> felt252;

    // @notice generate a new trait list with values and returns its id
    // @dev SHOULD revert if caller is not owner or an appproved operator of the contract
    //  SHOULD start storing values from index 1 as index 0 is default for attribute values
    //  MUST revert if any value in list from `values` is 0
    //  MUST revert if `values` list is of 0 length
    //  MUST emit `TraitListUpdate` event
    // @param `values` list of values that is to be added to trait list
    // @return list id of trait list that has been created
    fn generate_trait_list(ref self: TContractState, values: Span<felt252>) -> u64;

    // @notice append a single value to a trait list
    // @dev SHOULD revert if caller is not owner or an appproved operator of the contract
    //  MUST revert if `list_id` or `value` is 0
    //  MUST revert if `list_id` has yet to be generated
    //  MUST emit `TraitListUpdate` event
    // @param `list_id` the trait list to append values to
    // @param `value` the value that is to be added to trait list
    fn append_to_trait_list(ref self: TContractState, list_id: u64, value: felt252);

    // @notice append a batch of values to a trait list
    // @dev SHOULD revert if caller is not owner or an appproved operator of the contract
    //  MUST revert if `list_id` has yet to be generated or is 0
    //  MUST revert if any value in list from `values` is 0
    //  MUST revert if `values` list is of 0 length
    //  MUST emit `TraitListUpdate` event
    // @param `list_id` the trait list to append values to
    // @param `values` the list of value that is to be appended to the trait list
    fn append_batch_to_trait_list(ref self: TContractState, list_id: u64, values: Span<felt252>);

    // @notice OPTIONAL function to ammend values in a trait list
    // @dev SHOULD revert if caller is not owner or an appproved operator of the contract
    //  MUST revert if `list_id` is 0 or does not exist
    //  MUST revert if `index` is bigger than trait list length or is 0
    //  MUST revert if `value` is 0
    //  MUST emit `TraitListUpdate` event
    // @param `list_id` the id of the trait list of which to ammend values from
    // @param `index` the index of the trait list to change its value
    // @param `value` new value to swap into
    fn ammend_trait_list(ref self: TContractState, list_id: u64, index: felt252, value: felt252);
}
```

## Attribute Extensions

### Slot Attribute

**NOTES**:

-   extension for adding attributes to slots and allowing tokens minted under that slot to automtically inherit its values
-   if slot and token have clashing String attribute values slot attribute values are of piority
-   if slot and token have the same Number type attribute it is added together
-   this interface REQUIRES the `ERC3525` interface

### Events

#### SlotAttributeUpdate

MUST emit when an `attr_id` is added to a `slot_id` or when its corresponding value is changed

if `attr_id` is of type `String`:

-   `list_id == 0` `old_value`/`new_value` represents the actual value of the attribute
-   `list_id != 0` `old_value`/`new_value`represents the index of the trait list attached to the`attr_id`

```cairo
#[derive(Drop, starknet::Event)]
struct SlotAttributeUpdate {
    #[key]
    slot_id : u256,
    #[key]
    attr_id: u64,
    #[key]
    attr_type: AttrType,
    #[key]
    old_value : felt252,
    #[key]
    new_value : felt252
}
```

### interface

```cairo
#[starknet::interface]
trait IERC2114SlotAttribute<TContractState> {
    // @notice get the attribute value associated with the slot id
    // @dev if `attr_id` uses a trait list SHOULD still output the value acording to the trait list
    //       SHOULD NOT return the index from the trait list
    // @param `slot_id` slot to query the attributes value from
    // @param `attr_id` attribute id to query value from
    // @return the attribute value associated with the slot
    fn slot_attribute_value(self: @TContractState, slot_id: u256, attr_id: u64) -> felt252;

    // @notice get all attributes associated with `slot_id`
    // @param `slot_id` slot which to query all owned attributes id
    // @return an array of all `attr_id` attached to a slot
    fn slot_attributes_of(self: @TContractState, slot_id: u256) -> Span<u64>;

    // @notice sets a attribute and coresponding value to a slot id
    // @dev SHOULD revert if caller is not owner or an appproved operator of the contract
    //  MUST revert if slot_id is 0
    //  MUST revert if `attr_id` is 0 or attribute does not exist
    //  MUST revert if `value` is 0
    //  MUST revert if `value` is referencing an index in a trait list and the index is out of bounds
    //  MUST emit `SlotAttributeUpdate` event
    // @param `slot_id` the slot to which attribute should be added to
    // @param `attr_id` the attribute that should be added
    // @param `value` corresponding value for the slot attribute
    fn set_slot_attribute(ref self: TContractState, slot_id: u256, attr_id: u64, value: felt252);


    // @notice sets a batch of attributes and coresponding values to a slot id
    // @dev SHOULD revert if caller is not owner or an appproved operator of the contract
    //  MUST revert if slot_id is 0
    //  MUST revert if `attr_ids` and `values` length is not the same
    //  MUST revert if any of the `attr_ids` is 0 or attribute does not exist
    //  MUST revert if any value in `values` is 0
    //  MUST revert if any value in `values` is referencing an index in a trait list and the index is out of bounds
    //  MUST emit `SlotAttributeUpdate` event
    // @param `slot_id` the slot to which attributes should be added to
    // @param `attr_ids` the set of attributes that should be added
    // @param `values` the set of corresponding values for the slot attributes
    fn batch_set_slot_attribute(
        ref self: TContractState, slot_id: u256, attr_ids: Span<u64>, values: Span<felt252>
    );
}
```

## Inventory Extension

The ERC-2114 standard introduces a new approach to token attributes, enabling tokens to 'equip' other tokens by transfering one token within another. In doing so, the primary token can inherit the attributes of the embedded, or 'child', token. However, a limitation in the current design is the unrestricted nature of token transfers, which could lead to inadvertent inheritance of all attributes based solely on ownership. This extension addresses this limitation by distinguishing the tokens 'backpack' and 'inventory', providing a clear limitations on how many and which tokens can be inherited frrom through the use of their slots.

### BackPack

A token's 'backpack' denotes its storage , primarily showcasing balance, ownership, and approval of their owned tokens. Modifications to the backpack do not influence the token's traits.

### Inventory

A token's 'inventory' operates as a filter over the 'backpack', enabling the segregation of tokens based on their designated `slot_id`. This separation ensures that tokens are organized and interacted with, based on a specific slot criteria. Each `slot_id` comes with predetermined specifications and balance constraints, which shape the inventory mechanics for all tokens minted under that particular slot. For instance, tokens under slot 1 might be designed to accommodate three slot 2 tokens and a single slot 3 token. The inventory operates as a First-In-First-Out (FIFO) system. This will modify how the ERC2114 contract's `scalar_transfer_from` and `scalar_remove_from` functions work: they will now automatically add or remove tokens to the inventory, provided there's available room. Significantly, any token housed within the inventory automatically has their `attr_ids` inherited by the parent token.

### Attribute Inheritance Model

attributes that needs to be inherited must be explicitly set on the slot level, if attribute is of type Number, token will inherit from its inventory, by summing the attribiute value of the token, with the attribute value of all tokens within its inventory. if there is any conflicting String type attribute values from the same attribute, the reference tokens attribute value will take precedence over its inventory tokens attribute value. if the conflicting attribute exist on the inventory level, it will be based on a FIFO basis where the value will be drived from the first equipped token in the inventory that contains the queried attribute.

### Specification

**NOTES**

-   contract must implement 'ERC2114' and `ERC3525` interfaces
-   slot attributes are only inherited based on association not through inventory

### Events

#### InventorySlotCriteria

Must emit when slot criteria is set

```cairo
#[derive(Drop, PartialEq, starknet::Event)]
struct InventorySlotCriteria {
    #[key]
    slot_id: u256,
    #[key]
    criteria: u256,
    old_capacity: u64,
    new_capacity: u64
}
```

#### InventoryAttributes

Must emit when inventory atributes is changed

```cairo
#[derive(Drop, PartialEq, starknet::Event)]
struct InventoryAttributes {
    #[key]
    slot_id: u256,
    attr_ids: Span<u64>
}
```

#### InventoryUpdated

Must emit when token is added or removed from inventory

```cairo
#[derive(Drop, PartialEq, starknet::Event)]
struct InventoryUpdated {
    #[key]
    token_id: u256,
    #[key]
    criteria: u256,
    #[key]
    child_id: u256,
    #[key]
    old_bal: u64,
    #[key]
    new_bal: u64
}
```

### Interface

```cairo
#[starknet::interface]
trait IERC2114Inventory<TContractState> {
    // @notice gets the combined value of attribute associated with queried token and the
    //  value of attributes inherited from tokens in its inventory
    // @dev attribute value does not include slot attribute value of queried token or inventory tokens
    //  if inventory attributes and token attribute is type String and has conflicting values, token attribute
    //  takes piority
    //  if inventory attributes and token attribute is type Number sum them together
    //  only inherit attributes if they are in inventory attributes
    // @param `token_id` token to query combined attributes value from
    // @param `attr_id` attribute id to query combined value from
    // @return the value associated with token and its inventory
    fn equipped_attribute_value(self: @TContractState, token_id: u256, attr_id: u64) -> felt252;

    // @notice query if a token is contained in another tokens inventory
    // @dev MUST revert if `token_id` or 'child_id' do not exist
    // @param `token_id` the token to query inventory from
    // @param `child_id` the token to query existance in inventory
    // @return boolean value of existance of `child_id` in `token_id` inventory
    fn is_inside_inventory(self: @TContractState, token_id: u256, child_id: u256) -> bool;

    // @notice query the ammount of tokens currently in the tokens inventory based on slot criteria
    // @dev MUST revert if `token_id` does not exist
    // @param `token_id` the token to query inventory balance from
    // @param 'criteria` the slot criteria in inventory to query balance from
    // @return the ammount of tokens that are already stored in the inventory slot
    fn token_supply_in_inventory(self: @TContractState, token_id: u256, criteria: u256) -> u64;

    // @notice query a tokens inventory to get all tokens stored within it
    // @dev MUST revert if `token_id` does not exist
    // @param `token_id` the token too query inventory from
    // @return an array of all tokens held within the inventory
    fn inventory_of(self: @TContractState, token_id: u256) -> Span<u256>;

    // @notice gets all attributes that can be inheritted by a token base on its slot
    // @param `slot_id` slot to query attributes from
    // @return an array of attribute ids
    fn inventory_attributes_of(self: @TContractState, slot_id: u256) -> Span<u64>;

    // @notice query the ammount of tokens from slot `criteria` that can be
    //  stored in the `slot_id` inventory system
    // @param `slot_id` to query the inventory system from
    // @param `criteria` refers to a slot that has a capacity criteria set on it
    // @return the capacity of criteria set on slot_id
    fn slot_criteria_capacity(self: @TContractState, slot_id: u256, criteria: u256) -> u64;

    // @notice equip and unequip tokens to/from the tokens inventory
    // @dev caller MUST be either the owner or approved operator of the "final parent token"
    //   of the `token_id`. which means that approval falls onto the final token id; up the
    //   chain of token ownership that isnt currently owned by any token id.
    //  MUST revert if `token_id`  does not exist
    //  MUST revert if `token_id` is not the direct owner of `child_id`
    //  MUST emit InventoryUpdated event
    //  For Equipping:
    //      MUST revert if `child_id` is already in inventory
    //      MUST revert if inventory is at max capacity
    //  For UnEquipping:
    //      MUST revert if child_id is not equipped
    // @param `token_id` token to query inventory from
    // @param 'child_id` token to equip or unequip from `token_id` backpack
    // @param `equipped` set the child_id in inventory to true or false
    fn edit_inventory(
        ref self: TContractState, token_id: u256, child_id: u256, equipped: bool
    );

    // @notice sets a capacity for how many tokens of slot `criteria` that can be held in any token
    //  from slot `slot_id` inventory
    // @dev MUST revert if capacity is lower than what is already set for its criteria
    //  MUST revert if capacity is 0
    //  MUST emit InventorySlotCriteria event
    // @param `slot_id` to set the inventory system for
    // @param `criteria` a slot id to set a capacity on for `slot_id` inventory system
    // @param `capacity` the ammount of tokens of slot `critera` that can be stored in the inverntory
    //  of any token from `slot_id`
    fn set_slot_criteria(ref self: TContractState, slot_id: u256, criteria: u256, capacity: u64);

    // @notice sets a batch of attributes that should be inherited bt any token from `slot_id`
    // @dev MUST revert if attr_ids has id that does not exist
    //  MUST revert if attr_ids contains repeats
    //  MUST revert if same attr_ids sequence is already set
    //  MUST emit InventoryAttributes event
    // @param `slot_id` to set the inventory attributes for
    // @param `attr_ids` span of attributes to set to slot
    fn set_inventory_attributes(ref self: TContractState, slot_id: u256, attr_ids: Span<u64>);
}
```

### Inventory Manipulation

appart from using the native `edit_inventory` function for Equipping and UnEquipping tokens, inventory manipulation SHOULD also be done through ERC2114-compatible `scalar_transfer_from` and `scalar_remove_from` functions.

```cairo
// @notice used to equip tokens into the inventory
// @dev automatically check if `token_id` has inventory space for `to_token_id`
//  based on `to_token_id` slot as a criteria
//  MUST emit InventoryUpdated event if `to_token_id` is added to inventory
fn scalar_transfer_from(
    ref self: TContractState, from: ContractAddress, token_id: u256, to_token_id: u256
);

// @notice used to unequip tokens from inventory if it exist
// @dev automatically check if `token_id` is in `from_token_id` inventory
//  MUST emit InventoryUpdated event if `token_id` is removed from inventory
fn scalar_remove_from(ref self: TContractState, from_token_id: u256, token_id: u256);
```

## Implementation

## History

## Copyright

Copyright and related rights waived via [AGPL-3.0](../LICENSE-2114).
